apply plugin: "java"
apply plugin: "java-gradle-plugin"

archivesBaseName = 'gradle-shaderc-plugin'
version = '1.0-SNAPSHOT'
group = 'net.caffeinemc'

repositories {
    mavenCentral()
}

def lwjglVersion = "3.2.3"
def lwjglNatives

switch (org.gradle.internal.os.OperatingSystem.current()) {
    case org.gradle.internal.os.OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "natives-linux"
        break
    case org.gradle.internal.os.OperatingSystem.MAC_OS:
        lwjglNatives = "natives-macos"
        break
    case org.gradle.internal.os.OperatingSystem.WINDOWS:
        lwjglNatives = "natives-windows"
        break
}

dependencies {
    implementation "org.lwjgl:lwjgl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-shaderc:$lwjglVersion"

    runtimeOnly "org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-shaderc:$lwjglVersion:$lwjglNatives"

    implementation "commons-io:commons-io:2.6"
}

gradlePlugin {
    // Define the plugin
    plugins {
        compiler {
            id = 'net.caffeinemc.gradle-shaderc-plugin'
            implementationClass = 'net.caffeinemc.gradle.shaderc.ShaderPlugin'
        }
    }
}